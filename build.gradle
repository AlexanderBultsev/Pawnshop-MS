plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.6' apply false
    id 'io.spring.dependency-management' version '1.1.7' apply false
}

allprojects {
    group = 'com.pawnshop'
    version = '1.0.0'
    repositories {
        mavenCentral()
    }
    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(21)
        }
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

    ext {
        set('springCloudVersion', "2025.0.0")
    }

    dependencies {  // Общие зависимости
        testImplementation 'org.mockito:mockito-core' // Мокито
        testImplementation 'org.springframework.boot:spring-boot-starter-test' // Тесты
        testRuntimeOnly 'org.junit.platform:junit-platform-launcher' // Тесты
        testImplementation 'net.bytebuddy:byte-buddy-agent' // Агент для тестов
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
        }
    }

    tasks.named('test') {
        useJUnitPlatform()
        jvmArgs '-Xshare:off', '-javaagent:' + configurations.testRuntimeClasspath.find { it.name.contains('byte-buddy-agent') }
    }
}